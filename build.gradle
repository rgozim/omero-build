import com.google.common.base.CaseFormat

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "com.google.guava:guava:27.1-jre"
    }
}

task publishBinaryToMavenLocal {
    group "omero"
    description "Calls publish\${projet.name}BinaryPublicationToMavenLocal on all subprojects"
    dependsOn gradle.includedBuilds.collect {
        it.task(":publish${camelCaseName(it)}BinaryPublicationToMavenLocal")
    }
}

task publishBinaryAndSourcesToMavenLocal {
    group "omero"
    description "Calls publish\${projet.name}BinaryAndSourcesPublicationToMavenLocal on all subprojects"
    dependsOn gradle.includedBuilds.collect {
        it.task(":publish${camelCaseName(it)}BinaryAndSourcesPublicationToMavenLocal")
    }
}

task publishToMavenLocal {
    group "omero"
    description "Calls publishToMavenLocal on all subprojects"
    dependsOn gradle.includedBuilds*.task(':publishToMavenLocal')
}

task publish {
    group "omero"
    description "Calls publish on all subprojects"
    dependsOn gradle.includedBuilds*.task(':publish')
}

task clean {
    group "omero"
    description "Calls clean on all subprojects"
    dependsOn gradle.includedBuilds*.task(':clean')
}

task test {
    group "omero"
    description "Calls test on all subprojects"
    dependsOn gradle.includedBuilds*.task(':test')
}

task javadoc {
    group "omero"
    description "Calls javadoc on all subprojects"
    dependsOn gradle.includedBuilds*.task(':javadoc')
}

String camelCaseName(IncludedBuild ib) {
    CaseFormat.LOWER_HYPHEN.to(CaseFormat.LOWER_CAMEL, ib.name).capitalize()
}